/* Mountainside (10-10-17) */
/* Changes from 1.0: now regicide compatible */
/* Changes from 1.1: Middle now more round, simplified stuff */
/* Changes from 1.2: forest-cliff-water spacing added */
/* Changes from 1.3: Changed middle */

#const ICY_BEACH 37 

<PLAYER_SETUP>
random_placement

<LAND_GENERATION>
base_terrain SNOW
create_player_lands
{
	terrain_type GRASS_SNOW
	zone 1
	land_percent 100
	base_size 10
	border_fuzziness 15
	if TINY_MAP
		other_zone_avoidance_distance 10
	elseif SMALL_MAP
		other_zone_avoidance_distance 11
	elseif MEDIUM_MAP
		other_zone_avoidance_distance 12
	elseif LARGE_MAP
		other_zone_avoidance_distance 13
	elseif HUGE_MAP
		other_zone_avoidance_distance 14
	elseif GIGANTIC_MAP
		other_zone_avoidance_distance 15
	endif
}	
create_land /* middle */
{
	terrain_type ICE
	land_percent 100
	top_border 37
	bottom_border 37
	left_border 37
	right_border 37
	clumping_factor 15
	if TINY_MAP
		other_zone_avoidance_distance 10
	elseif SMALL_MAP
		other_zone_avoidance_distance 11
	elseif MEDIUM_MAP
		other_zone_avoidance_distance 12
	elseif LARGE_MAP
		other_zone_avoidance_distance 13
	elseif HUGE_MAP
		other_zone_avoidance_distance 14
	elseif GIGANTIC_MAP
		other_zone_avoidance_distance 15
	endif
}
create_land /* middle */
{
	terrain_type ICE
	land_percent 100
	top_border 37
	bottom_border 37
	left_border 37
	right_border 37
	clumping_factor 15
	if TINY_MAP
		other_zone_avoidance_distance 10
	elseif SMALL_MAP
		other_zone_avoidance_distance 11
	elseif MEDIUM_MAP
		other_zone_avoidance_distance 12
	elseif LARGE_MAP
		other_zone_avoidance_distance 13
	elseif HUGE_MAP
		other_zone_avoidance_distance 14
	elseif GIGANTIC_MAP
		other_zone_avoidance_distance 15
	endif
}

<ELEVATION_GENERATION>
if TINY_MAP
	create_elevation 5
elseif SMALL_MAP
	create_elevation 5
elseif MEDIUM_MAP
	create_elevation 6
elseif LARGE_MAP
	create_elevation 6
elseif HUGE_MAP
	create_elevation 7
elseif GIGANTIC_MAP
	create_elevation 7
endif
{
	number_of_tiles 10000
	number_of_clumps 1000
	base_terrain ICE
	set_scale_by_groups
}
create_elevation 3
{
	number_of_tiles 2500
	number_of_clumps 24
	base_terrain GRASS_SNOW
	set_scale_by_groups
}
	
<TERRAIN_GENERATION>
create_terrain DIRT_SNOW
{
	base_terrain ICE
	land_percent 100
	if TINY_MAP
		height_limits 5 5
	elseif SMALL_MAP
		height_limits 5 5
	elseif MEDIUM_MAP
		height_limits 6 6
	elseif LARGE_MAP
		height_limits 6 6
	elseif HUGE_MAP
		height_limits 7 7
	elseif GIGANTIC_MAP
		height_limits 7 7
	endif
}
create_terrain SNOW_FOREST 
{
	base_terrain DIRT_SNOW
	number_of_tiles 25
	number_of_clumps 2
	set_scale_by_groups
}
create_terrain ICY_BEACH 
{
	base_terrain GRASS_SNOW
	land_percent 1
	number_of_clumps 3
	set_scale_by_groups
	set_avoid_player_start_areas
	set_flat_terrain_only
	height_limits 0 0
	spacing_to_other_terrain_types 3
}
create_terrain WATER
{
	base_terrain ICY_BEACH
	land_percent 100
	number_of_clumps 1000
	set_scale_by_groups
	set_avoid_player_start_areas
	set_flat_terrain_only
	height_limits 0 0
	spacing_to_other_terrain_types 1
}
create_terrain SNOW_FOREST /* player primary forest */
{
	base_terrain GRASS_SNOW
	land_percent 15
	number_of_clumps 16
	set_avoid_player_start_areas
	spacing_to_other_terrain_types 3
	set_scale_by_groups
}
create_terrain SNOW
{
	base_terrain GRASS_SNOW
	land_percent 50
	number_of_clumps 32
	set_scale_by_groups
}

<OBJECTS_GENERATION>
create_object TOWN_CENTER
{
	set_place_for_every_player
	min_distance_to_players 0
	max_distance_to_players 0
}
create_object SCOUT
{
	set_place_for_every_player
	min_distance_to_players 7
	max_distance_to_players 9
}
create_object VILLAGER /* default is 3 */
{
	set_place_for_every_player
	min_distance_to_players 6
	max_distance_to_players 6
}
create_object GOLD /* primary */
{
	number_of_objects 7
	set_tight_grouping
	group_placement_radius 3
	set_place_for_every_player
	set_gaia_object_only
	min_distance_to_players 12
	max_distance_to_players 16
	min_distance_group_placement 7
}
create_object GOLD /* secondary */
{
	number_of_objects 4
	set_tight_grouping
	group_placement_radius 3
	set_place_for_every_player
	set_gaia_object_only
	min_distance_to_players 18
	max_distance_to_players 26
	min_distance_group_placement 7
	max_distance_to_other_zones 7
}
create_object GOLD /* tertiary */
{
	number_of_objects 4
	set_tight_grouping
	group_placement_radius 3
	set_place_for_every_player
	set_gaia_object_only
	min_distance_to_players 25
	max_distance_to_players 35
	min_distance_group_placement 7
	max_distance_to_other_zones 7
}
create_object STONE /* in center */
{  
	number_of_objects 6
	number_of_groups 4
	group_variance 1
	set_tight_grouping
	placement_radius 4
	set_gaia_object_only
	set_scaling_to_map_size
	min_distance_group_placement 2
	temp_min_distance_group_placement 5
	terrain_to_place_on DIRT_SNOW
}
if DEATH_MATCH
else
create_object SHEEP /* primary */
{
	number_of_objects 4
	set_loose_grouping
	set_place_for_every_player
	set_gaia_object_only
	min_distance_to_players 10
	max_distance_to_players 12
}
create_object SHEEP /* secondary */
{
	number_of_objects 2
	number_of_groups 2
	set_loose_grouping
	set_place_for_every_player
	set_gaia_object_only
	min_distance_to_players 14
	max_distance_to_players 30
}
create_object BOAR
{
	number_of_objects 2
	set_place_for_every_player
	set_gaia_object_only
	min_distance_to_players 16
	max_distance_to_players 22
}
create_object FORAGE
{
	number_of_objects 6
	set_tight_grouping
	group_placement_radius 4
	set_place_for_every_player
	set_gaia_object_only
	min_distance_to_players 10
	max_distance_to_players 12
}
create_object DEER
{
	number_of_objects 4
	set_tight_grouping
	set_place_for_every_player
	set_gaia_object_only
	min_distance_to_players 14
	max_distance_to_players 30
}
create_object WOLF
{
	number_of_objects 4
	set_place_for_every_player
	set_gaia_object_only
	min_distance_to_players 30
	max_distance_to_players 40
}
create_object SNOW_PINE_TREE /* straggler trees */
{
  	number_of_objects             2
  	set_gaia_object_only
  	set_place_for_every_player
  	min_distance_to_players       4
  	max_distance_to_players       5
  	min_distance_group_placement  2
}
create_object SNOW_PINE_TREE
{
  	number_of_objects             3
  	set_gaia_object_only
  	set_place_for_every_player
  	min_distance_to_players       6
 	max_distance_to_players       8
  	min_distance_group_placement  2
}
endif
create_object RELIC /* in center */
{  
	number_of_objects 10
	set_gaia_object_only
	terrain_to_place_on DIRT_SNOW
	temp_min_distance_group_placement 3
	set_scaling_to_map_size
}
create_object SHORE_FISH
{
	number_of_objects 100
	temp_min_distance_group_placement 5
}
if REGICIDE /* regicide objects */
create_object VILLAGER
{
  number_of_objects             7
  set_place_for_every_player
  min_distance_to_players       6
  max_distance_to_players       6
}
create_object KING
{
  set_place_for_every_player
  min_distance_to_players       6
  max_distance_to_players       6
}
create_object CASTLE
{
  set_place_for_every_player
  min_distance_to_players    10
  max_distance_to_players    10
}
endif 

<CLIFF_GENERATION>
if TINY_MAP
	min_number_of_cliffs 8
	max_number_of_cliffs 10
elseif SMALL_MAP
	min_number_of_cliffs 12
	max_number_of_cliffs 14
elseif MEDIUM_MAP
	min_number_of_cliffs 16
	max_number_of_cliffs 18
elseif LARGE_MAP
	min_number_of_cliffs 20
	max_number_of_cliffs 22
elseif HUGE_MAP
	min_number_of_cliffs 24
	max_number_of_cliffs 26
elseif GIGANTIC_MAP
	min_number_of_cliffs 28
	max_number_of_cliffs 30
endif
min_distance_cliff 5
min_length_of_cliff 5
max_length_of_cliff 15
