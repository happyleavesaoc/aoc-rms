/* Cluster (6-11-14) */
/* Changes from 1.0: forest-cliff spacing added */

start_random
	percent_chance 50
		#define HOT
	percent_chance 50
		#define NOT
end_random
		
<PLAYER_SETUP>
random_placement
ai_info_map_type OASIS 0 0 0

<LAND_GENERATION>
base_terrain GRASS2
create_player_lands
{
	terrain_type GRASS2
	land_percent 5
	base_size 15
	other_zone_avoidance_distance 1
}
create_land
{
	terrain_type ICE
	land_percent 25
	top_border 35
	bottom_border 35
	right_border 35
	left_border 35
	other_zone_avoidance_distance 1
}

<ELEVATION_GENERATION>
create_elevation 5
{
	base_terrain GRASS2
	number_of_tiles 1250
	number_of_clumps 24
	set_scale_by_groups
}

<TERRAIN_GENERATION>
create_terrain WATER
{
	base_terrain ICE
	land_percent 100
	if TINY_MAP
		spacing_to_other_terrain_types 10
	elseif SMALL_MAP
		spacing_to_other_terrain_types 12
	elseif MEDIUM_MAP
		spacing_to_other_terrain_types 14
	elseif LARGE_MAP
		spacing_to_other_terrain_types 16
	elseif HUGE_MAP
		spacing_to_other_terrain_types 18
	elseif GIGANTIC_MAP
		spacing_to_other_terrain_types 20
	endif
}
if HOT
	create_terrain PALM_DESERT
else
	create_terrain JUNGLE
endif
{
	base_terrain ICE
	land_percent 100
	number_of_clumps 1000
}	
if HOT
	create_terrain PALM_DESERT
else
	create_terrain JUNGLE
endif
{
	base_terrain GRASS2
	land_percent 5
	number_of_clumps 24
	set_scale_by_groups
	set_avoid_player_start_areas
  	spacing_to_other_terrain_types 3
}
create_terrain WATER
{
  base_terrain                   GRASS2
  spacing_to_other_terrain_types 3
if TINY_MAP
  number_of_tiles  800
elseif SMALL_MAP
  number_of_tiles  1200
elseif MEDIUM_MAP
  number_of_tiles  1600
elseif LARGE_MAP
  number_of_tiles  2400
elseif HUGE_MAP
  number_of_tiles  3200
else
  number_of_tiles  3200
endif
if TINY_MAP
  number_of_clumps 6
elseif SMALL_MAP
  number_of_clumps 9
elseif MEDIUM_MAP
  number_of_clumps 12
elseif LARGE_MAP
  number_of_clumps 16
elseif HUGE_MAP
  number_of_clumps 22
else
  number_of_clumps 22
endif
	clumping_factor 100
  set_avoid_player_start_areas     
}

create_terrain SHALLOW
{
  base_terrain                   WATER
land_percent 3
number_of_clumps 99
  set_avoid_player_start_areas     
}
if HOT
	create_terrain DIRT
	{
		base_terrain GRASS2
		land_percent 100
		number_of_clumps 1000
	}
	create_terrain DIRT
	{
		base_terrain GRASS2
		land_percent 100
		number_of_clumps 1000
	}
	create_terrain DESERT
	{
		base_terrain DIRT
		land_percent 15
		number_of_clumps 24
		set_scale_by_gorups
	}
endif
if HOT
	create_terrain DIRT
else
	create_terrain GRASS3
endif
{
	base_terrain ICE
	land_percent 100
	number_of_clumps 1000
}

<OBJECTS_GENERATION>
create_object TOWN_CENTER
{
	set_place_for_every_player
	min_distance_to_players 0
	max_distance_to_players 0
}
create_object VILLAGER
{
	set_place_for_every_player
	min_distance_to_players 6
	max_distance_to_players 6
}
create_object SCOUT
{
	set_place_for_every_player
	min_distance_to_players 7
	max_distance_to_players 9
}
create_object GOLD /* primary */
{
  	number_of_objects          7
  	group_placement_radius     3
  	set_tight_grouping
  	set_gaia_object_only
  	set_place_for_every_player
  	min_distance_to_players    12
  	max_distance_to_players    16
  	min_distance_group_placement  7
}
create_object STONE /* primary */
{
	number_of_objects 5
	group_placement_radius 3
	set_tight_grouping
	set_gaia_object_only
	set_place_for_every_player
	min_distance_to_players    12
  	max_distance_to_players    16
	min_distance_group_placement  7
}
create_object GOLD /* secondary */
{
	number_of_objects 5
	set_tight_grouping
	group_placement_radius 3
	set_place_for_every_player
	set_gaia_object_only
	min_distance_to_players 18
	max_distance_to_players 26
	min_distance_group_placement 7
	max_distance_to_other_zones 7
}
create_object STONE /* secondary */
{
	number_of_objects 4
	group_placement_radius 2
	set_tight_grouping
	set_gaia_object_only
	set_place_for_every_player
	min_distance_to_players 20
	max_distance_to_players 26
	min_distance_group_placement 7
	max_distance_to_other_zones 7
}
create_object GOLD /* extra */
{
	number_of_objects 4
	number_of_groups 3
	group_varience 1
	set_tight_grouping
	group_placement_radius 3
	set_gaia_object_only
	set_scaling_to_map_size
	min_distance_group_placement 3
	temp_min_distance_group_placement 20
	min_distance_to_players 30
}
create_object STONE /* extra */
{
	number_of_objects 4
	number_of_groups 2
	group_varience 1
	set_tight_grouping
	group_placement_radius 3
	set_gaia_object_only
	set_scaling_to_map_size
	min_distance_group_placement 3
	temp_min_distance_group_placement 20
	min_distance_to_players 30
}
if DEATH_MATCH
else
if HOT
	create_object SHEEP /* primary */
else
	create_object TURKEY
endif
{
   	number_of_objects 4
   	set_loose_grouping
   	set_gaia_object_only
   	set_place_for_every_player
   	min_distance_to_players    9
   	max_distance_to_players    11
}
if HOT
	create_object SHEEP /* secondary */
else
	create_object TURKEY
endif
{
   	number_of_objects 2
	number_of_groups 2
   	set_loose_grouping
   	set_gaia_object_only
   	set_place_for_every_player
   	min_distance_to_players    14
   	max_distance_to_players    30
}
if HOT
	create_object WOLF
else
	create_object JAGUAR
endif
{
	number_of_objects 3
	set_scaling_to_player_number
	set_gaia_object_only
	min_distance_to_players 30
	max_distance_to_players 40
}
create_object FORAGE 
{
	number_of_objects 6
	set_tight_grouping
	group_placement_radius 4
	set_gaia_object_only
	set_place_for_every_player
	min_distance_to_players 10
	max_distance_to_players 12
}
create_object BOAR
{
	number_of_objects 2
	set_place_for_every_player
	set_gaia_object_only
	min_distance_to_players 16
	max_distance_to_players 22
}
create_object DEER
{
	number_of_objects 4
	set_place_for_every_player
	set_gaia_object_only
	set_loose_grouping
	min_distance_to_players 18
	max_distance_to_players 24
}
create_object PALM_FOREST_TREE /* straggler trees */
{
  	number_of_objects             2
  	set_gaia_object_only
  	set_place_for_every_player
  	min_distance_to_players       4
  	max_distance_to_players       5
  	min_distance_group_placement  2
}
create_object PALM_FOREST_TREE 
{
  	number_of_objects             3
  	set_gaia_object_only
  	set_place_for_every_player
  	min_distance_to_players       6
 	max_distance_to_players       8
  	min_distance_group_placement  2
}
endif
create_object SHORE_FISH
{
  number_of_objects                   100
  set_scaling_to_map_size
  set_gaia_object_only
  temp_min_distance_group_placement  7
}
create_object SNAPPER
{
  number_of_objects                   100
  set_scaling_to_map_size
  set_gaia_object_only
 temp_min_distance_group_placement  12
}
create_object RELIC 
{
	number_of_objects 5
	min_distance_to_players 25
	set_gaia_object_only
	temp_min_distance_group_placement 25  
}
if REGICIDE /* regicide objects */
create_object VILLAGER
{
  number_of_objects             7
  set_place_for_every_player
  min_distance_to_players       6
  max_distance_to_players       6
}
create_object KING
{
  set_place_for_every_player
  min_distance_to_players       6
  max_distance_to_players       6
}
create_object CASTLE
{
  set_place_for_every_player
  min_distance_to_players    10
  max_distance_to_players    10
}
endif

<CLIFF_GENERATION>
min_length_of_cliff 5
max_length_of_cliff 8
min_distance_cliff 20
if TINY_MAP
	min_number_of_cliffs 5
	max_number_of_cliffs 7
elseif SMALL_MAP
	min_number_of_cliffs 9
	max_number_of_cliffs 11
elseif MEDIUM_MAP
	min_number_of_cliffs 13
	max_number_of_cliffs 15
elseif LARGE_MAP
	min_number_of_cliffs 17
	max_number_of_cliffs 19
elseif HUGE_MAP
	min_number_of_cliffs 21
	max_number_of_cliffs 23
elseif GIGANTIC_MAP
	min_number_of_cliffs 25
	max_number_of_cliffs 27
endif