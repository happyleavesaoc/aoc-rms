/* Cornucopia (6-7-17) */

if TINY_MAP
	#define MID_BORDER 30
	#define PLAYER_BORDER 5
elseif SMALL_MAP
	#define MID_BORDER 31
	#define PLAYER_BORDER 6
elseif MEDIUM_MAP
	#define MID_BORDER 32
	#define PLAYER_BORDER 7
elseif LARGE_MAP
	#define MID_BORDER 33
	#define PLAYER_BORDER 8
elseif HUGE_MAP
	#define MID_BORDER 34
	#define PLAYER_BORDER 9
elseif GIGANTIC_MAP
	#define MID_BORDER 35
	#define PLAYER_BORDER 10
endif

<PLAYER_SETUP>
random_placement

<LAND_GENERATION>
base_terrain GRASS
create_player_lands
{
	land_percent 1
	top_border 5
	bottom_border 5
	right_border 5
	left_border 5
	terrain_type GRASS
	base_size 9
}
create_land
{
	terrain_type WATER
	land_percent 100
	top_border 32
	bottom_border 32
	right_border 32
	left_border 32
}

<ELEVATION_GENERATION>
create_elevation 2
{
	base_terrain GRASS
	number_of_tiles 1250
	number_of_clumps 24
	set_scale_by_groups
}

<CLIFF_GENERATION>
min_length_of_cliff 5
max_length_of_cliff 8
min_distance_cliffs 10
if TINY_MAP
	min_number_of_cliffs 15
	max_number_of_cliffs 17
elseif SMALL_MAP
	min_number_of_cliffs 19
	max_number_of_cliffs 21
elseif MEDIUM_MAP
	min_number_of_cliffs 23
	max_number_of_cliffs 25
elseif LARGE_MAP
	min_number_of_cliffs 27
	max_number_of_cliffs 29
elseif HUGE_MAP
	min_number_of_cliffs 31
	max_number_of_cliffs 33
elseif GIGANTIC_MAP
	min_number_of_cliffs 35
	max_number_of_cliffs 37
endif

<TERRAIN_GENERATION>
create_terrain BEACH
{
	base_terrain WATER
	land_percent 100
}
create_terrain GRASS2
{
	base_terrain BEACH
	land_percent 100
	spacing_to_other_terrain_types 10
}
create_terrain WATER
{
	base_terrain BEACH
	land_percent 1
	number_of_clumps 24
	set_scale_by_groups
}
create_terrain BAMBOO
{
	base_terrain GRASS
	land_percent 10
	number_of_clumps 24
	set_scale_by_groups
	set_avoid_player_start_areas
	spacing_to_other_terrain_types 3
}
create_terrain JUNGLE
{
	base_terrain GRASS2
	land_percent 1
	number_of_clumps 10
	set_scale_by_groups
}
create_terrain DIRT
{
	base_terrain GRASS
	land_percent 10
	number_of_clumps 24
	set_scale_by_groups
}

<OBJECTS_GENERATION>
create_object TOWN_CENTER
{
	set_place_for_every_player
	min_distance_to_players 0
	max_distance_to_players 0
}
create_object VILLAGER
{
	set_place_for_every_player
	min_distance_to_players 6
	max_distance_to_players 6
}
create_object SCOUT
{
	set_place_for_every_player
	min_distance_to_players 7
	max_distance_to_players 9
}
create_object GOLD /* primary */
{
  	number_of_objects          7
  	group_placement_radius     3
  	set_tight_grouping
  	set_gaia_object_only
  	set_place_for_every_player
  	min_distance_to_players    12
  	max_distance_to_players    16
  	min_distance_group_placement  7
}
create_object STONE /* primary */
{
	number_of_objects 5
	group_placement_radius 3
	set_tight_grouping
	set_gaia_object_only
	set_place_for_every_player
	min_distance_to_players    12
  	max_distance_to_players    16
	min_distance_group_placement  7
}
create_object GOLD /* secondary */
{
	number_of_objects 5
	set_tight_grouping
	group_placement_radius 3
	set_place_for_every_player
	set_gaia_object_only
	min_distance_to_players 18
	max_distance_to_players 26
	min_distance_group_placement 7
	max_distance_to_other_zones 7
}
create_object STONE /* secondary */
{
	number_of_objects 4
	group_placement_radius 2
	set_tight_grouping
	set_gaia_object_only
	set_place_for_every_player
	min_distance_to_players 20
	max_distance_to_players 26
	min_distance_group_placement 7
	max_distance_to_other_zones 7
}
create_object GOLD /* extra */
{
	number_of_objects 4
	number_of_groups 3
	group_varience 1
	set_tight_grouping
	group_placement_radius 3
	set_gaia_object_only
	set_scaling_to_map_size
	min_distance_group_placement 3
	temp_min_distance_group_placement 20
	min_distance_to_players 30
}
create_object STONE /* extra */
{
	number_of_objects 4
	number_of_groups 2
	group_varience 1
	set_tight_grouping
	group_placement_radius 3
	set_gaia_object_only
	set_scaling_to_map_size
	min_distance_group_placement 3
	temp_min_distance_group_placement 20
	min_distance_to_players 30
}
if DEATH_MATCH
else
create_object DEER
{
	number_of_objects 5
	number_of_groups 3
	set_gaia_object_only
	set_loose_grouping
	set_scaling_to_map_size
	temp_min_distance_group_placement 4
	terrain_to_place_on GRASS2
}
create_object SHEEP /* primary */
{
   	number_of_objects 4
   	set_loose_grouping
   	set_gaia_object_only
   	set_place_for_every_player
   	min_distance_to_players    9
   	max_distance_to_players    11
}
create_object SHEEP /* secondary */
{
   	number_of_objects 2
	number_of_groups 2
   	set_loose_grouping
   	set_gaia_object_only
   	set_place_for_every_player
   	min_distance_to_players    14
   	max_distance_to_players    30
}
create_object FORAGE 
{
	number_of_objects 6
	set_tight_grouping
	group_placement_radius 4
	set_gaia_object_only
	set_place_for_every_player
	min_distance_to_players 10
	max_distance_to_players 12
}
create_object OAKTREE /* straggler trees */
{
  	number_of_objects             2
  	set_gaia_object_only
  	set_place_for_every_player
  	min_distance_to_players       4
  	max_distance_to_players       5
  	min_distance_group_placement  2
}
create_object OAKTREE 
{
  	number_of_objects             3
  	set_gaia_object_only
  	set_place_for_every_player
  	min_distance_to_players       6
 	max_distance_to_players       8
  	min_distance_group_placement  2
}
endif
create_object BAMBOO_TREE
{
	number_of_objects 20
	temp_min_distance_group_placement 5
	min_distance_to_players 20
	set_scaling_to_map_size
}
create_object SHORE_FISH
{
	number_of_objects 100
	set_gaia_object_only
	set_scaling_to_map_size
	temp_min_distance_group_placement 3
}
create_object RELIC 
{
	number_of_objects 5
	min_distance_to_players 25
	set_gaia_object_only
	temp_min_distance_group_placement 25  
}
if REGICIDE /* regicide objects */
create_object VILLAGER
{
  number_of_objects             7
  set_place_for_every_player
  min_distance_to_players       6
  max_distance_to_players       6
}
create_object KING
{
  set_place_for_every_player
  min_distance_to_players       6
  max_distance_to_players       6
}
create_object CASTLE
{
  set_place_for_every_player
  min_distance_to_players    10
  max_distance_to_players    10
}
endif