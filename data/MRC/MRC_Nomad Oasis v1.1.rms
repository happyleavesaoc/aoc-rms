/* Nomad Oasis (8-8-15) */
/* Changes from 1.0: different starting config for players */

<PLAYER_SETUP>
random_placement
nomad_resources

<LAND_GENERATION>
base_terrain GRASS
create_land
{
	terrain_type SHALLOW
	top_border 37
	bottom_border 37
	right_border 37
	left_border 37
}
create_player_lands
{
	base_terrain BEACH
	land_percent 1
	top_border 45
	bottom_border 45
	right_border 45
	left_border 45
}

<ELEVATION_GENERATION>

<TERRAIN_GENERATION>
create_terrain GRASS2
{
	base_terrain BEACH
	land_percent 100
	number_of_clumps 1000
}
create_terrain DESERT
{
	base_terrain GRASS
	land_percent 100
	number_of_clumps 1000
	spacing_to_other_terrain_types 5
}
create_terrain DIRT
{
	base_terrain DESERT
	land_percent 100
	number_of_clumps 1000
	set_scale_by_groups
	if TINY_MAP
		spacing_to_other_terrain_types 15
	elseif SMALL_MAP
		spacing_to_other_terrain_types 18
	elseif MEDIUM_MAP
		spacing_to_other_terrain_types 21
	elseif LARGE_MAP
		spacing_to_other_terrain_types 24
	elseif HUGE_MAP
		spacing_to_other_terrain_types 27
	elseif GIGANTIC_MAP
		spacing_to_other_terrain_types 30
	endif
}
create_terrain PALM_DESERT
{
	base_terrain DESERT
	land_percent 5
	number_of_clumps 250
	set_scale_by_groups
}
create_terrain GRASS3
{
	base_terrain DIRT
	land_percent 10
	number_of_clumps 24
	set_scale_by_groups
	spacing_to_other_terrain_types 3
}
create_terrain PALM_DESERT
{
	base_terrain GRASS3
	land_percent 100
	number_of_clumps 1000
	set_scale_by_groups
}
create_terrain WATER
{
	base_terrain SHALLOW
	land_percent 100
	spacing_to_other_terrain_types 1
}
create_terrain MED_WATER
{
	base_terrain WATER
	land_percent 100
	number_of_clumps 1000
	set_scale_by_groups
	if TINY_MAP
		spacing_to_other_terrain_types 1
	elseif SMALL_MAP
		spacing_to_other_terrain_types 2
	elseif MEDIUM_MAP
		spacing_to_other_terrain_types 3
	elseif LARGE_MAP
		spacing_to_other_terrain_types 4
	elseif HUGE_MAP
		spacing_to_other_terrain_types 5
	elseif GIGANTIC_MAP
		spacing_to_other_terrain_types 6
	endif
}

<OBJECTS_GENERATION>
create_object VILLAGER
{
	set_place_for_every_player
	terrain_to_place_on BEACH
}
create_object TRANSPORT_SHIP
{
	set_place_for_every_player
	terrain_to_place_on MED_WATER
}	
create_object MAP_REVEALER
{
	set_place_for_every_player
	number_of_objects 1000
	set_scaling_to_map_size
	temp_min_distance_group_placement 3
	terrain_to_place_on MED_WATER
}
create_object GOLD
{
	number_of_objects 2
	number_of_groups 100
	set_tight_grouping
	set_scaling_to_map_size
	min_distance_group_placement 3
	temp_min_distance_group_placement 15
	terrain_to_place_on GRASS
}
create_object STONE
{
	number_of_objects 2
	number_of_groups 100
	set_tight_grouping
	set_scaling_to_map_size
	min_distance_group_placement 3
	temp_min_distance_group_placement 15
	terrain_to_place_on GRASS
}
create_object GOLD /* outer */
{
	number_of_objects 5
	number_of_groups 8
	set_tight_grouping
	group_placement_radius 2
	set_scaling_to_map_size
	min_distance_group_placement 7
	temp_min_distance_group_placement 20
	max_distance_to_other_zones 7
	terrain_to_place_on DIRT
}
create_object STONE /* outer */
{
	number_of_objects 4
	number_of_groups 6
	set_tight_grouping
	group_placement_radius 2
	set_scaling_to_map_size
	min_distance_group_placement 7
	temp_min_distance_group_placement 20
	max_distance_to_other_zones 7
	terrain_to_place_on DIRT
}
create_object SNAPPER
{
	number_of_objects 100
	set_gaia_object_only
	temp_min_distance_group_placement 5
	set_scaling_to_map_size
	max_distance_to_other_zones 4
}
create_object MARLIN1
{
	number_of_objects 100
	set_gaia_object_only
	temp_min_distance_group_placement 5
	set_scaling_to_map_size
	terrain_to_place_on BEACH
}
create_object RELIC 
{
	number_of_objects 5
	min_distance_to_players 25
	set_gaia_object_only
	temp_min_distance_group_placement 25  
}
if DEATH_MATCH
else
create_object TURKEY
{
	number_of_objects 2
	number_of_groups 100
	set_gaia_object_only
	set_loose_grouping
	temp_min_distance_group_placement 10
	terrain_to_place_on GRASS
}
create_object BOAR /* outer */
{
	number_of_objects 3
	number_of_groups 2
	set_scaling_to_map_size
	set_loose_grouping
	set_gaia_object_only
	min_distance_group_placement 15
	temp_min_distance_group_placement 30
	max_distance_to_other_zones 7
	terrain_to_place_on DIRT
}
create_object DEER /* outer */
{
	number_of_objects 5
	number_of_groups 4
	set_scaling_to_map_size
	set_loose_grouping
	set_gaia_object_only
	min_distance_group_placement 15
	temp_min_distance_group_placement 30
	max_distance_to_other_zones 7
	terrain_to_place_on DIRT
}
endif
if REGICIDE /* regicide objects */
create_object VILLAGER
{
  number_of_objects             7
  set_place_for_every_player
  min_distance_to_players       6
  max_distance_to_players       6
}
create_object KING
{
  set_place_for_every_player
  min_distance_to_players       6
  max_distance_to_players       6
}
endif