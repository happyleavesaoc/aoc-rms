/*
	@author Biz
*/

<PLAYER_SETUP>
random_placement

<LAND_GENERATION>
base_terrain WATER

create_land {
	terrain_type LEAVES
	left_border 15
	right_border 15
	bottom_border 15
	top_border 15
	land_percent 50
	clumping_factor 100
	border_fuzziness 8
	other_zone_avoidance_distance 8
}

create_player_lands {
	terrain_type GRASS
	land_percent 20
	base_size 13
	other_zone_avoidance_distance 25
	clumping_factor 100
	border_fuzziness 100
}

<ELEVATION_GENERATION>
create_elevation 3 {
	base_terrain LEAVES
	number_of_tiles 200
	number_of_clumps 50
	set_scale_by_size
	set_scale_by_groups
}

<TERRAIN_GENERATION>
create_terrain MED_WATER {
	base_terrain WATER
	land_percent 99
	number_of_clumps 999
	spacing_to_other_terrain_types 3
}

create_terrain DEEP_WATER {
	base_terrain MED_WATER
	land_percent 99
	number_of_clumps 999
	spacing_to_other_terrain_types 4
}

create_terrain BAMBOO {
	base_terrain GRASS
	land_percent 3
	number_of_clumps 6
	set_scale_by_groups
	clumping_factor 100
	set_avoid_player_start_areas
	spacing_to_other_terrain_types 1
}

create_terrain GRASS2 {
	base_terrain GRASS
	land_percent 99
	number_of_clumps 999
	spacing_to_other_terrain_types 2
}

create_terrain BAMBOO {
	base_terrain LEAVES
	land_percent 5
	number_of_clumps 15
	set_scale_by_groups
	clumping_factor 100
	spacing_to_other_terrain_types 2
}

create_terrain GRASS3 {
	base_terrain LEAVES
	land_percent 99
	number_of_clumps 999
	spacing_to_other_terrain_types 2
}

create_terrain GRASS {
	base_terrain LEAVES
	land_percent 99
	number_of_clumps 999
}

<CONNECTION_GENERATION>

if TINY_MAP
	create_connect_all_players_land {
		replace_terrain WATER SHALLOW
		replace_terrain MED_WATER SHALLOW
		terrain_cost WATER 10
		terrain_cost MED_WATER 100
		terrain_size SHALLOW 4 0
	}
elseif SMALL_MAP
	create_connect_all_players_land {
		replace_terrain WATER SHALLOW
		replace_terrain MED_WATER SHALLOW
		terrain_cost WATER 10
		terrain_cost MED_WATER 100
		terrain_size SHALLOW 2 0
	}
else
	create_connect_teams_lands {
		replace_terrain WATER SHALLOW
		replace_terrain MED_WATER SHALLOW
		terrain_cost WATER 10
		terrain_cost MED_WATER 100
		if MEDIUM_MAP
			terrain_size SHALLOW 2 0
		elseif LARGE_MAP
			terrain_size SHALLOW 1 0
		else
			terrain_size SHALLOW 1 0
		endif
	}
endif

<OBJECTS_GENERATION>
create_object SNAPPER {
	number_of_objects 1
	number_of_groups 999
	group_placement_radius 1
	temp_min_distance_group_placement 11
	min_distance_group_placement 5
	set_gaia_object_only
	terrain_to_place_on MED_WATER
}

create_object FISH_DORADO {
	number_of_objects 1
	number_of_groups 999
	group_placement_radius 1	
	temp_min_distance_group_placement 15
	min_distance_group_placement 5
	set_gaia_object_only
	terrain_to_place_on MED_WATER
}

create_object TUNA {
	number_of_objects 1
	number_of_groups 999
	group_placement_radius 1	
	temp_min_distance_group_placement 21
	min_distance_group_placement 5
	set_gaia_object_only
	terrain_to_place_on MED_WATER
}

create_object MARLIN1 {
	number_of_objects 1
	number_of_groups 999
	group_placement_radius 1
	temp_min_distance_group_placement 8
	min_distance_group_placement 6
	set_gaia_object_only
	terrain_to_place_on DEEP_WATER
}

create_object MARLIN2 {
	number_of_objects 1
	number_of_groups 999
	group_placement_radius 1	
	temp_min_distance_group_placement 12
	min_distance_group_placement 6
	set_gaia_object_only
	terrain_to_place_on DEEP_WATER
}

create_object SHORE_FISH {
	number_of_objects 2
	group_variance 1
	set_loose_grouping
	number_of_groups 999
	temp_min_distance_group_placement 12
	set_gaia_object_only
	terrain_to_place_on WATER
}

create_object GOLD {
	number_of_objects 5
	number_of_groups 2
	set_scaling_to_player_number
	set_tight_grouping
	set_gaia_object_only
	terrain_to_place_on GRASS3
	if TINY_MAP
		max_distance_to_other_zones 8
	elseif SMALL_MAP
		max_distance_to_other_zones 10
	elseif MEDIUM_MAP
		max_distance_to_other_zones 12
	elseif LARGE_MAP
		max_distance_to_other_zones 15
	else
		max_distance_to_other_zones 20
	endif
		
	min_distance_group_placement 7
}

create_object STONE {
	number_of_objects 4
	number_of_groups 2
	set_scaling_to_player_number
	set_tight_grouping
	set_gaia_object_only
	terrain_to_place_on GRASS3
	max_distance_to_other_zones 3
	temp_min_distance_group_placement 15
	min_distance_group_placement 7
}

create_object TOWN_CENTER {
	set_place_for_every_player
	group_placement_radius 18
	min_distance_to_players 0
	max_distance_to_players	0
}

create_object VILLAGER {
	set_place_for_every_player
	min_distance_to_players	6
	max_distance_to_players	6
}

if REGICIDE
	create_object KING {
		set_place_for_every_player
		min_distance_to_players 9
		max_distance_to_players 9
	}
else
	create_object SCOUT {
		set_place_for_every_player
		min_distance_to_players 9
		max_distance_to_players 9
	}
endif

create_object FORAGE {
	number_of_objects 6
	number_of_groups 1
	set_gaia_object_only
	set_place_for_every_player
	set_tight_grouping
	terrain_to_place_on GRASS2
	min_distance_to_players 8
	max_distance_to_players 12
	max_distance_to_other_zones 3
	min_distance_group_placement 3
}

create_object SHEEP {
	number_of_objects 1
	number_of_groups 4
	set_tight_grouping
	set_gaia_object_only
	set_place_for_every_player
	min_distance_to_players 2
	max_distance_to_players 4
}

create_object BOAR {
	number_of_objects 2
	set_gaia_object_only
	set_place_for_every_player
	min_distance_to_players 15
	max_distance_to_players 20
}

create_object SHEEP {
	number_of_objects 2
	number_of_groups 2
	group_placement_radius 2
	set_gaia_object_only
	set_place_for_every_player
	min_distance_to_players 10
	max_distance_to_players 20
	terrain_to_place_on GRASS2
}

create_object GOLD {
	number_of_objects 5
	number_of_groups 1
	set_gaia_object_only
	set_place_for_every_player
	set_tight_grouping
	terrain_to_place_on GRASS2
	min_distance_to_players 9
	max_distance_to_players 15
	max_distance_to_other_zones 8
	min_distance_group_placement 7
}

create_object STONE {
	number_of_objects 5
	number_of_groups 1
	set_gaia_object_only
	set_place_for_every_player
	set_tight_grouping
	terrain_to_place_on GRASS2
	min_distance_to_players 9
	max_distance_to_players 18
	max_distance_to_other_zones 6
	min_distance_group_placement 7
}

create_object PALMTREE {
	number_of_objects 2
	set_gaia_object_only
	set_place_for_every_player
	min_distance_to_players 4
	max_distance_to_players 5
	min_distance_group_placement 2
}

create_object PALMTREE {
	number_of_objects 3
	set_gaia_object_only
	set_place_for_every_player
	min_distance_to_players 6
	max_distance_to_players 8
	min_distance_group_placement 2
}

create_object DEER {
	number_of_objects 4
	number_of_groups 1
	set_gaia_object_only
	set_place_for_every_player
	set_tight_grouping
	min_distance_to_players 18
	max_distance_to_players 25
}

create_object RELIC 
{
	number_of_objects 5
	min_distance_to_players 25
	set_gaia_object_only
	temp_min_distance_group_placement 25  
}

